# 中山大学 本科生实验报告

实验课程：操作系统原理实验

实验名称：Lab3 从实模式到保护模式

专业名称：计算机科学与技术

学生姓名：刘森元

学生学号：21307289

实验地点：广州校区东校园 实验中心 D501

报告时间：2023-04-01



***由于本人使用的环境如下：***

***Apple Inc. MacBook Pro 2021***

***M1 Pro (Apple Silicon)***

***导致部分流程与反馈与实验指导书相异，望周知。***

***使用的文献、软件、包大部分以超连接形式给出了原址。***



本实验报告基于[**一个支点撬动操作系统大山**](https://gitee.com/nelsoncheung/sysu-2023-spring-operating-system/tree/main)撰写。



## 1. 实验概述

> 在本节中，同学们将会学习到如何从 16 位的实模式跳转到 32 位的保护模式，然后在平坦模式下运行 32 位程序。同时，同学们将学习到如何使用 I/O 端口和硬件交互，为后面保护模式编程打下基础。

## 2. 实验任务

Task 1: 课后习题 9

Task 2: 课后习题 10

Task 3: 课后习题 11

Task 4: 课后习题 12

## 3. 实验步骤/实验结果

### Task 1: 课后习题 9

> 复现"加载 bootloader "一节，说说你是怎么做的并提供结果截图，也可以参考 Ucore、Xv6 等系统源码，实现自己的 LBA 方式的磁盘访问

在本节中，我们将 Lab2 中输出 Hello World 部份的代码放入到 bootloader 中，然后在 MBR 中加载 bootloader 到内存，并跳转到 bootloader 的起始地址执行。

目前，我们的内存地址安排如下，bootloader 被安排在 MBR 之后，预留 5 个扇区的空间。因此，我们假设 bootloader 的不大于 5 个扇区。

| **name**   | **start** | **length**       | **end** |
| ---------- | --------- | ---------------- | ------- |
| MBR        | 0x7c00    | 0x200 (512B)     | 0x7e00  |
| bootloader | 0x7e00    | 0xa00 (512B * 5) | 0x8800  |

创建 *bootloader.asm* 与 *mbr.asm* 并编译写入到 *hd.img* 中

```shell
nasm -f bin bootloader.asm -o bootloader.bin
dd if=bootloader.bin of=hd.img bs=512 count=5 seek=1 conv=notrunc
nasm -f bin mbr.asm -o mbr.bin
dd if=mbr.bin of=hd.img bs=512 count=1 seek=0 conv=notrunc
```

使用 qemu 运行，结果如下

![image-20230402200721775](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230402200721775.png)

成功复现了示例结果

*bootloader.asm*

```assembly
org 0x7e00
[bits 16]
mov ax, 0xb800
mov gs, ax
mov ah, 0x03 ;青色
mov ecx, bootloader_tag_end - bootloader_tag
xor ebx, ebx
mov esi, bootloader_tag
output_bootloader_tag:
    mov al, [esi]
    mov word[gs:bx], ax
    inc esi
    add ebx,2
    loop output_bootloader_tag
jmp $ ; 死循环

bootloader_tag db 'run bootloader'
bootloader_tag_end:
```

*mbr.asm*

```assembly
org 0x7c00
[bits 16]
xor ax, ax ; eax = 0
; 初始化段寄存器, 段地址全部设为0
mov ds, ax
mov ss, ax
mov es, ax
mov fs, ax
mov gs, ax

; 初始化栈指针
mov sp, 0x7c00
mov ax, 1                ; 逻辑扇区号第0~15位
mov cx, 0                ; 逻辑扇区号第16~31位
mov bx, 0x7e00           ; bootloader的加载地址
load_bootloader:
    call asm_read_hard_disk  ; 读取硬盘
    inc ax
    cmp ax, 5
    jle load_bootloader
jmp 0x0000:0x7e00        ; 跳转到bootloader

jmp $ ; 死循环

asm_read_hard_disk:                           
; 从硬盘读取一个逻辑扇区

; 参数列表
; ax=逻辑扇区号0~15位
; cx=逻辑扇区号16~28位
; ds:bx=读取出的数据放入地址

; 返回值
; bx=bx+512

    mov dx, 0x1f3
    out dx, al    ; LBA地址7~0

    inc dx        ; 0x1f4
    mov al, ah
    out dx, al    ; LBA地址15~8

    mov ax, cx

    inc dx        ; 0x1f5
    out dx, al    ; LBA地址23~16

    inc dx        ; 0x1f6
    mov al, ah
    and al, 0x0f
    or al, 0xe0   ; LBA地址27~24
    out dx, al

    mov dx, 0x1f2
    mov al, 1
    out dx, al   ; 读取1个扇区

    mov dx, 0x1f7    ; 0x1f7
    mov al, 0x20     ;读命令
    out dx,al

    ; 等待处理其他操作
  .waits:
    in al, dx        ; dx = 0x1f7
    and al,0x88
    cmp al,0x08
    jnz .waits                         
    

    ; 读取512字节到地址ds:bx
    mov cx, 256   ; 每次读取一个字，2个字节，因此读取256次即可          
    mov dx, 0x1f0
  .readw:
    in ax, dx
    mov [bx], ax
    add bx, 2
    loop .readw
      
    ret

times 510 - ($ - $$) db 0
db 0x55, 0xaa
```

### Task 2: 课后习题 10

> 在"加载bootloader"一节中，我们使用了LBA28的方式来读取硬盘。此时，我们只要给出逻辑扇区号即可，但需要手动去读取I/O端口。然而，BIOS提供了实模式下读取硬盘的中断，其不需要关心具体的I/O端口，只需要给出逻辑扇区号对应的磁头（Heads）、扇区（Sectors）和柱面（Cylinder）即可，又被称为CHS模式。现在，同学们需要将LBA28读取硬盘的方式换成CHS读取，同时给出逻辑扇区号向CHS的转换公式。最后说说你是怎么做的并提供结果截图，参考资料如下。
>
> - [LBA 向 CHS 模式的转换](https://blog.csdn.net/G_Spider/article/details/6906184)
> - [int 13h 中断](https://blog.csdn.net/brainkick/article/details/7583727)
>
> 其中，关键参数如下。
>
> | 参数                             | 数值 |
> | -------------------------------- | ---- |
> | 驱动器号（DL 寄存器）            | 80h  |
> | 每磁道扇区数                     | 63   |
> | 每柱面磁头数（每柱面总的磁道数） | 18   |
>

根据上述参考资料可知：

> 以 C、H、S 分别表示当前硬盘的柱面号、磁头号、扇区号，CS、HS、SS 分别表示起始柱面号、磁头号、扇区号，PS 表示每磁道扇区数，PH表示每柱面总的磁道数。 
>
> 则 C/H/S 到 LBA 的转换公式为: LBA = (C – CS) × PH × PS + (H – HS) × PS + (S – SS) 
>
> LBA到C/H/S的转换公式为: C = LBA // (PH × PS) + CS; H = (LBA // PS) % PH + HS; S = LBA // PS + SS 

一般情况下，CS = 0，HS = 0，SS = 1，PS = 63，PH = 18，所以需要读取的 1-5 扇区的 C/H/S 分别为0/0/2, 0/0/3, 0/0/4, 0/0/5, 0/0/6

使用 `int13h` 的 bios 中断，功能号为 `02h`，以实现读扇区的功能，其中参数定义为

| 寄存器 | 描述       |
| ------ | ---------- |
| AH     | 02H        |
| AL     | 扇区数     |
| CH     | 柱面       |
| bCL    | 扇区       |
| DH     | 磁头       |
| DL     | 驱动器     |
| ES:BX  | 缓冲区地址 |
| DL     | 80h        |

故可修改 Task 1 代码，重新编译并使用 qemu 运行，可得以下结果。

![image-20230402210203611](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230402210203611.png)

### Task 3: 课后习题 11

> 复现“进入保护模式”一节，使用 gdb 或其他 debug 工具在进入保护模式的4个重要步骤上设置断点，并结合代码、寄存器的内容等来分析这 4 个步骤，最后附上结果截图。gdb 的使用可以参考 appendix 的 “debug with gdb and qemu” 部份。

1. 准备 GDT,用 `ldgt` 加载 GDTR 信息

   ![image-20230402213938335](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230402213938335.png)

   可得到如下信息：

   | 起始地址 | 终止地址 | 数值               | 描述及含义                                                  |
   | -------- | -------- | ------------------ | ----------------------------------------------------------- |
   | 0x8800   | 0x8808   | 0x0000000000000000 | 空描述符，与 LDTR 占位有关                                  |
   | 0x8808   | 0x8810   | 0x00cf92000000ffff | 基地址为 0，段界限为 0xFFFFF，粒度为 4KB，存储器段描述符    |
   | 0x8810   | 0x8818   | 0x0040960000000000 | 基地址为 0，界限 0，粒度为 1 个字节，堆栈段描述符           |
   | 0x8818   | 0x8820   | 0x0040920b80007fff | 基地址为 0x000B8000，界限 0x07FFF，粒度为字节，显存段描述符 |
   | 0x8820   | 0x8828   | 0x00cf98000000ffff | 基地址为 0，段界限为 0xFFFFF，粒度为 4KB，代码段描述符      |

2. 打开第 21 根地址线

   ![image-20230402214520168](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230402214520168.png)

3. 开启 cr0 的保护模式标识位

   ![image-20230402214759655](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230402214759655.png)

4. 进入保护模式

   ![image-20230402214851950](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230402214851950.png)

程序完成。

### Task 4: 课后习题 12

> 在进入保护模式后，编写并执行一个自己的32位汇编程序。
>
> 使用两种不同的自定义颜色和一个自定义的起始位置(x,y)，使得bootloader加载后，在显示屏坐标(x,y)处开始输出自己的学号+姓名拼音首字母缩写，要求相邻字符前景色和背景色必须是相互对调的。公告图片中提供了学号为21307233，姓名为宋小宝，自定义位置(12,12)的输出样式，仅供参考。代码实现框架可参考实验2和实验3的MBR程序。 

![image-20230402223020331](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230402223020331.png)

*bootloader.asm*

```assembly
%include "boot.inc"
org 0x7e00
[bits 16]

;空描述符
mov dword [GDT_START_ADDRESS+0x00],0x00
mov dword [GDT_START_ADDRESS+0x04],0x00  

;创建描述符，这是一个数据段，对应0~4GB的线性地址空间
mov dword [GDT_START_ADDRESS+0x08],0x0000ffff    ; 基地址为0，段界限为0xFFFFF
mov dword [GDT_START_ADDRESS+0x0c],0x00cf9200    ; 粒度为4KB，存储器段描述符 

;建立保护模式下的堆栈段描述符      
mov dword [GDT_START_ADDRESS+0x10],0x00000000    ; 基地址为0x00000000，界限0x0 
mov dword [GDT_START_ADDRESS+0x14],0x00409600    ; 粒度为1个字节

;建立保护模式下的显存描述符   
mov dword [GDT_START_ADDRESS+0x18],0x80007fff    ; 基地址为0x000B8000，界限0x07FFF 
mov dword [GDT_START_ADDRESS+0x1c],0x0040920b    ; 粒度为字节

;创建保护模式下平坦模式代码段描述符
mov dword [GDT_START_ADDRESS+0x20],0x0000ffff    ; 基地址为0，段界限为0xFFFFF
mov dword [GDT_START_ADDRESS+0x24],0x00cf9800    ; 粒度为4kb，代码段描述符 

;初始化描述符表寄存器GDTR
mov word [pgdt], 39      ;描述符表的界限   
lgdt [pgdt]
      
in al,0x92                         ;南桥芯片内的端口 
or al,0000_0010B
out 0x92,al                        ;打开A20

cli                                ;中断机制尚未工作
mov eax,cr0
or eax,1
mov cr0,eax                        ;设置PE位
      
;以下进入保护模式
jmp dword CODE_SELECTOR:protect_mode_begin

;16位的描述符选择子：32位偏移
;清流水线并串行化处理器
[bits 32]           
protect_mode_begin:                              

mov eax, DATA_SELECTOR                     ;加载数据段(0..4GB)选择子
mov ds, eax
mov es, eax
mov eax, STACK_SELECTOR
mov ss, eax
mov eax, VIDEO_SELECTOR
mov gs, eax

mov ecx, name_tag_end - name_tag
mov ebx, 80 * 24
add ebx, 12 * 2
mov esi, name_tag
mov ah, 0x74
output_name_tag:
    xor ah, 0x33
    mov al, [esi]
    mov word[gs:ebx], ax
    add ebx, 2
    inc esi
    loop output_name_tag

jmp $ ; 死循环

pgdt dw 0
     dd GDT_START_ADDRESS

name_tag db '21307289 liusenyuan'
name_tag_end:

times 2560 - ( $ - $$ ) db 0

```

## 4. 实验总结/心得体会

MBR 是由 BIOS 加载的，其余扇区数据需要由 MBR 加载。加载方式有两种：LBA（逻辑块寻址模式）和 CHS（立体寻址模式）。LBA 需要按照硬件协议通过 I/O 接口加载；CHS 是 BIOS 提供的功能，但比较老。

进入实模式的四个步骤：

1. 准备 GDT，用 ldgt 指令加载 GDTR 信息
2. 打开第 21 根地址线
3. 开启 cr0 的保护模式标识位
4. 远跳转，进入保护模式

GDT 是在内存中记录段描述符的表，每项 8 字节。进入保护模式后段寄存器保存段选择子

本次实验从头到尾复现了实模式跳转到保护模式的过程，过程中遇到了不少困难与挑战。

例如在编写自己的汇编程序时，出现了程序未成功载入的状况，但在根据教程纠错后能成功执行并完成要求。
