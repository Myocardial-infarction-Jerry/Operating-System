# 中山大学 本科生实验报告

实验课程：操作系统原理实验

实验名称：实验入门

专业名称：计算机科学与技术

学生姓名：刘森元

学生学号：21307289

实验地点：广州校区东校园 实验中心 D501

报告时间：2023-03-15



***由于本人使用的环境如下：***

***Apple Inc. MacBook Pro 2021***

***M1 Pro (Apple Silicon)***

***导致部分流程与反馈与实验指导书相异，望周知。***

***使用的文献、软件、包大部分以超连接形式给出了原址。***



本实验报告基于[**一个支点撬动操作系统大山**](https://gitee.com/nelsoncheung/sysu-2023-spring-operating-system/tree/main)撰写。

## 1. 实验要求

1. 学习到x86汇编、计算机的启动过程、IA-32处理器架构和字符显存原理。
2. 根据所学的知识编写程序，然后让计算机在启动后加载运行，以此增进对计算机启动过程的理解，为后面编写操作系统加载程序奠定基础。
3. 学习如何使用gdb来调试程序的基本方法。

## 2. 实验步骤/实验过程

### 操作系统的启动 Hello World

#### 编写 MBR

编写 MBR 的代码，在 MBR 被加载到内存地址 `0x7c00` 后，向屏幕输出浅品红色的学号，代码如下所示。**~~*（问就是更喜欢品红色）*~~**

```assembly
org 0x7c00
[bits 16]
xor ax, ax ; eax = 0
; 初始化段寄存器, 段地址全部设为0
mov ds, ax
mov ss, ax
mov es, ax
mov fs, ax
mov gs, ax

; 初始化栈指针
mov sp, 0x7c00
mov ax, 0xb800
mov gs, ax


mov ah, 0x0d ; 浅品红色
mov al, '2'
mov [gs:2 * 0], ax

mov al, '1'
mov [gs:2 * 1], ax

mov al, '3'
mov [gs:2 * 2], ax

mov al, '0'
mov [gs:2 * 3], ax

mov al, '7'
mov [gs:2 * 4], ax

mov al, '2'
mov [gs:2 * 5], ax

mov al, '8'
mov [gs:2 * 6], ax

mov al, '9'
mov [gs:2 * 7], ax

jmp $ ; 死循环

times 510 - ($ - $$) db 0
db 0x55, 0xaa
```

使用 nasm 汇编器将 asm 文件编译成二进制文件

```shell
nasm -f bin ID.asm -o ID.bin
```

创建虚拟磁盘 `hd.img` ，并将 MBR 写入首扇区

```shell
qemu-img create hd.img 10m
dd if-mbr.bin of=hd.img bs=512 count=1 seek=0 conv=notrunc
```

启动 qemu 模拟计算机启动

```shell
qemu-system-i386 -hda hd.img -serial null -parallel stdio
```

使用上述命令会出现下列错误：

![image-20230315171550387](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230315171550387.png)

查阅 qemu 文档可知，由于 qemu 默认使用 gtk 启动，而本人使用环境为 Ubuntu Live Server 22.04.2 LTS，仅支持 Terminal 输出，故会出现 `gtk initialization failed` ，故改为使用下列指令：

```shell
qemu-system-i386 -hda hd.img -serial null -parallel stdio -display curses
```

使用 Terminal 输出，出现下列结果

![image-20230319164709097](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230319164709097.png)

成功输出了浅品红色的 `21307289`

### debug

使用 gdb 配合 qemu 对生成的 Hello World MBR 进行 debug

为了使得gdb能够找到debug信息，我们需要生成符号表。注意，生成符号表的时候需要将 `mbr.asm` 开头的 `org` 伪指令删去~~（注释）~~

```assembly
; org 0x7c00
[bits 16]
xor ax, ax ; eax = 0
; 初始化段寄存器, 段地址全部设为0
mov ds, ax
mov ss, ax
mov es, ax
mov fs, ax
mov gs, ax

; 初始化栈指针
mov sp, 0x7c00
mov ax, 0xb800
mov gs, ax


mov ah, 0x0d ; 浅品红色
mov al, '2'
mov [gs:2 * 0], ax

mov al, '1'
mov [gs:2 * 1], ax

mov al, '3'
mov [gs:2 * 2], ax

mov al, '0'
mov [gs:2 * 3], ax

mov al, '7'
mov [gs:2 * 4], ax

mov al, '2'
mov [gs:2 * 5], ax

mov al, '8'
mov [gs:2 * 6], ax

mov al, '9'
mov [gs:2 * 7], ax

jmp $ ; 死循环

times 510 - ($ - $$) db 0
db 0x55, 0xaa
```

```shell
nasm -o ID.o -g -f elf32 ID.asm 
ld -o ID.symbol -melf_i386 -N ID.o -Ttext 0x7c00
```

为了使 qemu 支持 gdb 进行 debug，需要向启动命令中加入 `-s -S` 参数

```sh
qemu-system-i386 -hda hd.img -s -S -serial null -parallel stdio -display curses
```

在另一个 Terminal 中启动 gdb 并进行符号表加载与断点调试

```sh
gdb
(gdb) target remote:1234
(gdb) add-symbol-file mbr.symbol 0x7c00
(gdb) layout src
(gdb) b *0x7c00
(gdb) c
.
```

![image-20230319165129335](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230319165129335.png)

![image-20230319164709097](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230319164709097.png)

可见 qemu 成功在 `0x7c00` 暂停，gdb 发出继续运行指令后继而输出浅品红色 `21307289`

### 课后思考题

14. 字符弹射程序。请编写一个字符弹射程序，其从点 (2,0) 处开始向右下角45度开始射出，遇到边界反弹，反弹后按 45 度角射出，方向视反弹位置而定。同时，你可以加入一些其他效果，如变色，双向射出等。注意，你的程序应该不超过 510 字节，否则无法放入 MBR 中被加载执行。静态示例效果如下。动态效果见视频 `assignment/assignment-4-example.mp4`。

    由于程序实现需要众多变量，寄存器无法完成存储任务，故我在内存中开辟空间用来储存变量。

    每次执行输出时，依次进行设置光标、获取字符、打印字符、打印信息、更新速度、更新坐标、更新颜色、延时、

    切换光标等任务。

    ```assembly
    org 0x7c00 ; mbr
    [bits 16]
    ; 初始化
    mov si, string ; 加载字符表
    mov di, 0 ; 光标选择
    mov bh, 0 ; 设置页号
    again:
    ; 设置光标
    mov dh, [y + di]
    mov dl, [x + di]
    mov ah, 02h
    int 10h
    ; 获取字符
    lodsb
    cmp al, 0
    jne print
    mov si, string ; 重置字符指针
    lodsb
    ; 打印字符
    print:
    mov cx, 1 ; 输出字符数
    mov bl, [color + di] ; 颜色
    mov ah, 09h
    int 10h
    ; 打印信息
    pushad
    mov cl, 25 ; 初始化循环变量
    mov si, info ; 初始化学号地址
    mov bl, 02h ; 前景色绿色
    infop:
    cmp cl, 0 ; cl大于零时继续循环
    je infoe
    mov ah, 02h
    mov bh, 0
    mov dh, 0
    mov dl, 25 ; 计算列位置
    sub dl, cl
    add dl, 27
    int 10h
    push cx ; 保存cx
    mov ah, 09h
    lodsb ; 获取学号字符
    mov cx, 1
    int 10h
    pop cx
    dec cl ; cx自减
    jmp infop
    infoe:
    popad
    ; 列坐标更新
    cmp dl, 79 ; 边界检测
    je rb
    cmp dl, 0
    je lb
    jmp col
    rb: ; 右边界
    mov [vx + di], byte -1
    jmp col
    lb: ; 左边界
    mov [vx + di], byte 1
    col: ; 更新列坐标
    mov al, [vx + di]
    add dl, al
    mov [x + di], dl
    ; 行坐标更新
    cmp dh, 24 ; 边界检测
    je bb
    cmp dh, 0
    je ub
    jmp row
    bb: ; 下边界
    mov [vy + di], byte -1
    jmp row
    ub: ; 上边界
    mov [vy + di], byte 1
    row: ; 更新列坐标
    mov al, [vy + di]
    add dh, al
    mov [y + di], dh
    ; 颜色更新
    mov al, [color + di]
    inc al
    mov [color + di], al
    ; 光标切换
    xor di, 1
    ; 延时
    push dx
    mov ah, 86h
    mov cx, 0
    mov dx, 0x2710 ; 10ms 
    int 15h
    pop dx
    jmp again ; 循环
    string db "lliiuusseennyyuuaann2211330077228899", 0
    info db "---liusenyuan21307289---"
    vx db 1, -1
    vy db 1, 1
    x db 0, 79
    y db 2, 2
    color db 0, 0fh
    times 510 - ($ - $$) db 0
    db 0x55, 0xaa
    ```
    输出效果如下：

    ![image-20230319175504335](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230319175504335.png)

16. **使用中断移动并输出光标位置**

    编写汇编程序，依次完成移动光标、读取光标位置、按照格式输出坐标即可得到如下结果：

    ```assembly
    org 0x7c00 ; mbr
    [bits 16]
    ; 光标置于(8, 8)
    mov ah, 02h
    mov bh, 0
    mov dh, 8
    mov dl, 8
    int 10h
    ; 读取光标位置
    mov ah, 03h
    int 10h
    mov si, dx ; 行列
    mov bl, 0fh ; 亮白
    ; 输出
    mov ah, 09h
    mov al, '('
    mov cx, 1
    int 10h
    mov ah, 02h
    mov bh, 0
    mov dh, 8
    mov dl, 9
    int 10h
    mov ax, si
    mov cl, 8
    shr ax, cl
    add al, '0'
    mov ah, 09h
    mov cx, 1
    int 10h
    mov ah, 02h
    mov bh, 0
    mov dh, 8
    mov dl, 0ah
    int 10h
    mov ah, 09h
    mov al, ','
    mov cx, 1
    int 10h
    mov ah, 02h
    mov bh, 0
    mov dh, 8
    mov dl, 0bh
    int 10h
    mov ax, si
    add al, '0'
    mov ah, 09h
    mov cx, 1
    int 10h
    mov ah, 02h
    mov bh, 0
    mov dh, 8
    mov dl, 0ch
    int 10h
    mov ah, 09h
    mov al, ')'
    mov cx, 1
    int 10h
    jmp $ ; 死循环
    times 510 - ($ - $$) db 0
    db 0x55, 0xaa
    ```

    输出结果如下：

    ![image-20230319180832221](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230319180832221.png)

    **使用中断打印学号**

    由于学号字符较多，因此设置了 `number` 变量存储学号，并用 `len` 存储长度，使用循环完成输出

    ```assembly
    org 0x7c00 ; mbr
    [bits 16]
    mov cl, [len] ; 初始化循环变量
    mov si, number ; 初始化学号地址
    mov bl, 0fh ; 前景色亮白
    again:
    cmp cl, 0 ; cl大于零时继续循环
    je end
    mov ah, 02h
    mov bh, 0
    mov dh, 8
    mov dl, [len] ; 计算列位置
    sub dl, cl
    add dl, 8
    int 10h
    push cx ; 保存cx
    mov ah, 09h
    lodsb ; 获取学号字符
    mov cx, 1
    int 10h
    pop cx
    dec cl ; cx自减
    jmp again
    end:
    jmp $ ; 死循环
    number db '21307289'
    len db 8
    times 510 - ($ - $$) db 0
    db 0x55, 0xaa
    ```

    输出效果如下：

    ![image-20230319181152331](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230319181152331.png)

    **使用中断回显键盘输入**

    根据参考资料，置 `ah` 为功能码 0 并执行 `int 16h` 即可等待键盘输入。在键盘按下的时候中断返回，`al` 中将存放按下的字符 ASCII 码（输入为扩展码时 `al` 为 0，扩展码位于 `ah`）。简单起见，该程序只处理普通可打印字符，也就是直接回显。同时根据终端大小实现了自动换行。

    ```assembly
    org 0x7c00 ; mbr
    [bits 16]
    ; 初始化
    mov dl, 0 ; 列
    mov dh, 8 ; 行
    mov bh, 0 ; 页码
    mov bl, 0fh ; 亮白色
    mov cx, 1 ; 输出字符数
    mov ah, 02h
    int 10h ; 设置光标
    again:
    ; 等待输入
    mov ah, 0
    int 16h ; 等待键盘输入
    ; 字符回显
    mov ah, 09h
    int 10h
    ; 移动光标
    cmp dl, 79
    je next_line
    inc dl
    jmp move_cursor
    next_line:
    cmp dh, 25
    je first_line
    mov dl, 0
    inc dh
    jmp move_cursor
    first_line:
    mov dx, 0
    move_cursor:
    mov ah, 02h
    int 10h
    jmp again ; 重复输入
    times 510 - ($ - $$) db 0
    db 0x55, 0xaa
    ```

    输出结果如下：

    ![image-20230319191243316](/Users/qiu_nangong/Library/Application Support/typora-user-images/image-20230319191243316.png)

17. ```assembly
    your_if:
    mov eax, [a1] ; load a1
    cmp eax, 12
    jl C1
    cmp eax, 24
    jl C2
    C3: ; a1 >= 24
    sal eax, 4
    mov [if_flag], eax
    jmp CE
    C2: ; 12 <= a1 < 24 
    mov edx, 24
    sub edx, eax
    imul eax, edx
    mov [if_flag], eax
    jmp CE
    C1: ; a1 < 12
    sar eax, 1
    add eax, 1
    mov [if_flag], eax
    CE:
    
    your_while:
    
    L1:
    mov edx, [a2]
    cmp edx, 12
    jl WE
    call my_random
    mov edx, [a2]
    mov edi, edx
    sub edi, 12
    mov ebx, [while_flag]
    mov [ebx + edi], al
    sub edx, 1
    mov [a2], edx
    jmp L1
    WE:
    
    your_function:
    mov esi, [your_string]
    L2:
    lodsb
    cmp al, 0
    je FE
    pushad
    push ax
    call print_a_char
    pop ax
    popad
    jmp L2
    FE:
    ret
    ```

## 3. 总结

### MBR 与 qemu 相关知识

BIOS 在完成自检后将控制权交给MBR程序引导启动 OS 。 MBR 程序位于第一存储设备的首扇区，共 512 字节。执行前会检查 MBR 最后两字节是否为 `0x55, 0xAA` 来确认能否启动。确认后就会把MBR复制到内存的 `0x7c00` 处运行。qemu 显示屏大小为 25x80，其显示矩阵被映射到内存的 `0xB8000` 到 `0xBFFFF` 处，每个字符占用两字节，低字节为 ASCII 码，高字节为颜色，其中前四位为背景色，后四位为前景色。

### 相关指令

```shell
nasm -f bin *.asm -o *.bin
qemu-img create *.img 10m
dd if=*.bin of=*.img bs=512 seek=0 count=1 conv=notrunc
qemu-system-i386 -hda *.img -serial null -parallel stdio -display curses
```

### 中断信号

**int10h**

| 功能                       | 功能号 | 参数                                         | 返回值                                           |
| -------------------------- | ------ | -------------------------------------------- | ------------------------------------------------ |
| 设置光标位置               | AH=02H | BH=页码，DH=行，DL=列                        | 无                                               |
| 获取光标位置和形状         | AH=03H | BX=页码                                      | AX=0，CH=行扫描开始，CL=行扫描结束，DH=行，DL=列 |
| 在当前光标位置写字符和属性 | AH=09H | AL=字符，BH=页码，BL=颜色，CX=输出字符的个数 | 无                                               |

**int16h**

根据参考资料，置 `ah` 为功能码 `0` 并执行 `int 16h` 即可等待键盘输入。在键盘按下的时候中断返回，`al` 中将存放按下的字符 ASCII 码（输入为扩展码时 `al` 为 `0`，扩展码位于 `ah` ）。简单起见，该程序只处理普通可打印字符，也就是直接回显。同时根据终端大小实现了自动换行。

**int15h**

`int 15h` 中断的 `ah=86h` 功能码可以实现延时 `cx:dx` 微秒
